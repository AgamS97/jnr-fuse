plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
    id "com.jfrog.bintray" version "1.1"
    id 'maven'
}

repositories {
    jcenter()
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}

shadowJar {
    relocate 'org.objectweb.asm', 'org.objectweb.asm.shaded'
}


version = '0.3'
group = 'com.github.serceman'


if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}


dependencies {
    compile 'com.github.jnr:jnr-ffi:2.0.7'
    compile 'com.github.jnr:jnr-posix:3.0.23'
    compile 'com.github.jnr:jnr-constants:0.9.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

bintray {
    user = 'serce'
    key = ''
    configurations = ['archives']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'jnr-fuse'
        version {
            name = '0.3'
        }
    }
}
